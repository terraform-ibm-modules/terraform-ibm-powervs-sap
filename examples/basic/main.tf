locals {
  ibm_pvs_zone_region_map = {
    "syd04"    = "syd"
    "syd05"    = "syd"
    "eu-de-1"  = "eu-de"
    "eu-de-2"  = "eu-de"
    "lon04"    = "lon"
    "lon06"    = "lon"
    "tok04"    = "tok"
    "us-east"  = "us-east"
    "us-south" = "us-south"
    "dal12"    = "us-south"
    "tor01"    = "tor"
    "osa21"    = "osa"
    "sao01"    = "sao"
    "mon01"    = "mon"
  }

  ibm_pvs_zone_cloud_region_map = {
    "syd04"    = "au-syd"
    "syd05"    = "au-syd"
    "eu-de-1"  = "eu-de"
    "eu-de-2"  = "eu-de"
    "lon04"    = "eu-gb"
    "lon06"    = "eu-gb"
    "tok04"    = "jp-tok"
    "us-east"  = "us-east"
    "us-south" = "us-south"
    "dal12"    = "us-south"
    "tor01"    = "ca-tor"
    "osa21"    = "jp-osa"
    "sao01"    = "br-sao"
    "mon01"    = "ca-tor"
  }
}

#####################################################
# PVS SAP Instance Deployment example for SAP SYSTEM with new private network
#####################################################

# There are discrepancies between the region inputs on the powervs terraform resource, and the vpc ("is") resources
provider "ibm" {
  region           = lookup(local.ibm_pvs_zone_region_map, var.pvs_zone, null)
  zone             = var.pvs_zone
  ibmcloud_api_key = var.ibmcloud_api_key != null ? var.ibmcloud_api_key : null
}

provider "ibm" {
  alias            = "ibm-is"
  region           = lookup(local.ibm_pvs_zone_cloud_region_map, var.pvs_zone, null)
  zone             = var.pvs_zone
  ibmcloud_api_key = var.ibmcloud_api_key != null ? var.ibmcloud_api_key : null
}

#####################################################
# Create a new PowerVS infrastructure from scratch
#####################################################

locals {

  def_access_host_or_ip            = "not_used"
  def_internet_services_host_or_ip = "not_used"
  def_private_services_host_or_ip  = "not_used"

  def_os_image_distro = "SLES"

  squid_config = merge(var.squid_proxy_config, {
    "squid_enable"      = var.configure_proxy
    "server_host_or_ip" = var.squid_proxy_config["squid_proxy_host_or_ip"] != null && var.squid_proxy_config["squid_proxy_host_or_ip"] != "" ? var.squid_proxy_config["squid_proxy_host_or_ip"] : local.def_internet_services_host_or_ip
  })
  dns_forwarder_config = merge(var.dns_forwarder_config, {
    "dns_enable"        = var.configure_dns_forwarder
    "server_host_or_ip" = var.dns_forwarder_config["dns_forwarder_host_or_ip"] != null && var.dns_forwarder_config["dns_forwarder_host_or_ip"] != "" ? var.dns_forwarder_config["dns_forwarder_host_or_ip"] : local.def_private_services_host_or_ip
  })
  ntp_forwarder_config = merge(var.ntp_forwarder_config, {
    "ntp_enable"        = var.configure_ntp_forwarder
    "server_host_or_ip" = var.ntp_forwarder_config["ntp_forwarder_host_or_ip"] != null && var.ntp_forwarder_config["ntp_forwarder_host_or_ip"] != "" ? var.ntp_forwarder_config["ntp_forwarder_host_or_ip"] : local.def_private_services_host_or_ip
  })
  nfs_config = merge(var.nfs_server_config, {
    "nfs_enable"        = var.configure_nfs_server
    "server_host_or_ip" = var.nfs_server_config["nfs_server_host_or_ip"] != null && var.nfs_server_config["nfs_server_host_or_ip"] != "" ? var.nfs_server_config["nfs_server_host_or_ip"] : local.def_private_services_host_or_ip
  })

  pvs_service_name = "${var.prefix}-${var.pvs_service_name}"
  pvs_sshkey_name  = "${var.prefix}-${var.pvs_ssh_key_name}"
}

# Security Notice
# The private key generated by this resource will be stored unencrypted in your Terraform state file.
# Use of this resource for production deployments is not recommended.
# Instead, generate a private key file outside of Terraform and distribute it securely to the system where
# Terraform will be run.

resource "tls_private_key" "tls_key" {
  algorithm = "RSA"
  rsa_bits  = 4096
}

resource "ibm_is_ssh_key" "ssh_key" {
  provider   = ibm.ibm-is
  name       = "${var.prefix}-${var.pvs_ssh_key_name}"
  public_key = trimspace(tls_private_key.tls_key.public_key_openssh)
}

module "resource_group" {
  source = "git::https://github.com/terraform-ibm-modules/terraform-ibm-resource-group.git?ref=v1.0.1"
  # if an existing resource group is not set (null) create a new one using prefix
  resource_group_name          = var.resource_group == null ? "${var.prefix}-resource-group" : null
  existing_resource_group_name = var.resource_group
}

module "powervs_infratructure" {
  source                   = "git::https://github.com/terraform-ibm-modules/terraform-ibm-powervs-infrastructure.git?ref=v1.6.0"
  access_host_or_ip        = local.def_access_host_or_ip
  pvs_zone                 = var.pvs_zone
  ssh_private_key          = trimspace(tls_private_key.tls_key.private_key_openssh)
  pvs_resource_group_name  = module.resource_group.resource_group_name
  ssh_public_key           = ibm_is_ssh_key.ssh_key.public_key
  reuse_cloud_connections  = var.reuse_cloud_connections
  pvs_service_name         = local.pvs_service_name
  tags                     = var.resource_tags
  pvs_sshkey_name          = local.pvs_sshkey_name
  pvs_management_network   = var.pvs_management_network
  pvs_backup_network       = var.pvs_backup_network
  transit_gateway_name     = var.transit_gateway_name
  cloud_connection_count   = var.cloud_connection_count
  cloud_connection_speed   = var.cloud_connection_speed
  cloud_connection_gr      = var.cloud_connection_gr
  cloud_connection_metered = var.cloud_connection_metered
  squid_config             = local.squid_config
  dns_forwarder_config     = local.dns_forwarder_config
  ntp_forwarder_config     = local.ntp_forwarder_config
  nfs_config               = local.nfs_config
}

locals {
  networks = [var.pvs_management_network["name"], var.pvs_backup_network["name"]]
}

#####################################################
# Deploy SAP systems
#####################################################

module "sap_systems" {
  depends_on                 = [module.powervs_infratructure]
  source                     = "../../"
  pvs_zone                   = var.pvs_zone
  pvs_resource_group_name    = module.resource_group.resource_group_name
  pvs_service_name           = local.pvs_service_name
  pvs_sshkey_name            = local.pvs_sshkey_name
  pvs_sap_network_name       = var.pvs_sap_network_name
  pvs_sap_network_cidr       = var.pvs_sap_network_cidr
  pvs_additional_networks    = local.networks
  pvs_cloud_connection_count = var.cloud_connection_count

  pvs_share_number_of_instances  = var.pvs_sap_share_instance_config["number_of_instances"]
  pvs_share_image_name           = var.pvs_sap_share_instance_config["sap_image_name"]
  pvs_share_instance_name        = "${var.prefix}-${var.pvs_sap_share_instance_config["name-suffix"]}"
  pvs_share_memory_size          = var.pvs_sap_share_instance_config["memory_size"]
  pvs_share_number_of_processors = var.pvs_sap_share_instance_config["number_of_processors"]
  pvs_share_cpu_proc_type        = var.pvs_sap_share_instance_config["cpu_proc_type"]
  pvs_share_storage_config       = var.pvs_sap_share_storage_config

  pvs_hana_instance_name  = "${var.prefix}-${var.pvs_sap_hana_instance_config["name-suffix"]}"
  pvs_hana_image_name     = var.pvs_sap_hana_instance_config["sap_image_name"]
  pvs_hana_sap_profile_id = var.pvs_sap_hana_instance_config["sap_hana_profile_id"]
  pvs_hana_storage_config = var.pvs_sap_hana_storage_config

  pvs_netweaver_number_of_instances  = var.pvs_sap_netweaver_instance_config["number_of_instances"]
  pvs_netweaver_image_name           = var.pvs_sap_netweaver_instance_config["sap_image_name"]
  pvs_netweaver_instance_name        = "${var.prefix}-${var.pvs_sap_netweaver_instance_config["name-suffix"]}"
  pvs_netweaver_memory_size          = var.pvs_sap_netweaver_instance_config["memory_size"]
  pvs_netweaver_number_of_processors = var.pvs_sap_netweaver_instance_config["number_of_processors"]
  pvs_netweaver_cpu_proc_type        = var.pvs_sap_netweaver_instance_config["cpu_proc_type"]
  pvs_netweaver_storage_config       = var.pvs_sap_netweaver_storage_config

  access_host_or_ip = local.def_access_host_or_ip
  ssh_private_key   = trimspace(tls_private_key.tls_key.private_key_openssh)
  os_image_distro   = local.def_os_image_distro
  configure_os      = false
}
